# Copyright 2014 The Go Authors. All rights reserved.
# Use of this source code is governed by a BSD-style
# license that can be found in the LICENSE file.

# User-defined functions.

op i x = iota x
i 5
	1 2 3 4 5

op avg x = (+/x)/rho x
avg iota 11
	6

op n largest x = n take x[down x]
3 largest 7 1 3 24 1 5 12 5 51
	51 24 12

# Args are not globals
x = 100
op i x = iota x
i 3; x
	1 2 3 100

# Local non-args are locals if assigned first.
x = 100
op double u = x = u; x*2
double 3; x
	6 100

# Local non-args are globals if read first.
x = 100
op double u = x; x = u; x*2
double 3; x
	6 3

x = 100
op double u = x = x + u; x*2
double 3; x
	206 103

# Vector assignments can be locals.
x y = 10 20
op f v = x y = v; x+y
f 1 2
x y
	3
	10 20

# Vector reads can trigger globals.
x y = 10 20
op f v = x y; x y = v; x+y
f 1 2
x y
	3
	1 2

# in g calling f, f used to assign to global y but read from g's y.
op f x = y = 99; y
op g y = f y
g 42
y = 10
g 42
	99
	99

# Mention unary before use
op bar x = foo x
op foo x = -x
bar 3
		-3

# Mention binary before use
op x bar y = -x foo y
op x foo y = x + y
3 bar 4
		-7

# Multiline function definition
op a x b =
 y = a*b
 y = y*y
 y

3 x 4
	144

# Program printing
# Many old checks, most no longer really needed as source is verbatim but
# we keep them because why delete them?

# Simple.
op a x b = a + b
)op x
	op a x b = a + b

# Vector arguments
op f (b (c d)) = b + c + d
)op f
	op f (b (c d)) = b + c + d

op a f (b (c d)) = a + b + c + d
)op f
	op a f (b (c d)) = a + b + c + d

op (x y) f (b (c d)) = x + y + b + c + d
)op f
	op (x y) f (b (c d)) = x + y + b + c + d

# Nested vectors in the body
op f x = 1 2 (3 (4 x))
)op f
	op f x = 1 2 (3 (4 x))

# Character escapes and other formatting details.
op x a =
 'abc\u0001\U0010FFFF\\\'\a\b\f\n\r\t\v\n'
 'a'
 1.5
 1.5 'a'
 'a' 'b' 'c'

)op x
	op x a =
	 'abc\u0001\U0010FFFF\\\'\a\b\f\n\r\t\v\n'
	 'a'
	 1.5
	 1.5 'a'
	 'a' 'b' 'c'
	#

op x a = (3+2)*(3+2)
)op x
	op x a = (3+2)*(3+2)

op x a = abs (3+2)
)op x
	op x a = abs (3+2)

op x a = 3 + abs -2
)op x
	op x a = 3 + abs -2

op x a = (sqrt 4)*a
)op x
	op x a = (sqrt 4)*a

op x a = a*(sqrt 4)*(sqrt 4)
)op x
	op x a = a*(sqrt 4)*(sqrt 4)

op a x = 1 (sqrt x) 2
)op a
	op a x = 1 (sqrt x) 2

op f x = (iota x)[1]
)op f
	op f x = (iota x)[1]

op f x = x[1] + x[2]
)op f
	op f x = x[1] + x[2]

# We can define a function with the same name as a zero variable.
x = 0
op x n = n
x 3
	3

# We can define a function with the same name as a builtin.
op sqrt x = x
sqrt 4
	4

# It will be restored in a new context.
sqrt 4
	2

# Reduce using a user-defined binary operator.
op a binop b = a*b
(binop/iota 10) == */iota 10
	1

# Inner product using a user-defined binary operator on the left.
op a add b = a + b
2 3 4 add.* 1 2 3
	20

# Inner product using a user-defined binary operator on the right.
op a mul b = a * b
2 3 4 +.mul 1 2 3
	20

# Inner product using a user-defined binary operator on both left and right.
op a add b = a + b
op a mul b = a * b
2 3 4 add.mul 1 2 3
2 3 4 mul.add 1 2 3
	20
	105

# Outer product using user-defined binary operator.
op a mul b = a * b
2 3 4 o.mul 10 11
	20 22
	30 33
	40 44

# Builtin names are fine as parameter names.
op sin x cos = (sin+cos) cos 7
sin pi/2
3 x 2
	1
	5 2 7

# Sum the digits (only) in the rhs.
op sumdigits x = t = text x; +/ (code (t in '0123456789') sel t) - code '0'
sumdigits 99
sumdigits iota 10
sumdigits '23 skidoo'
	18
	46
	5

# Primes less <= N
op primes N = (not T in T o.* T) sel T = 1 drop iota N
primes 100
	2 3 5 7 11 13 17 19 23 29 31 37 41 43 47 53 59 61 67 71 73 79 83 89 97

# Conditional execution
# Factorial and gcd
op fac n =
 n <= 1 : 1
 n * fac n - 1

op a gcd b =
 # Greatest Common Divisor
 a == b: a
 a > b: b gcd a-b
 a gcd b-a

fac 10
1562 gcd fac 11
	3628800
	22

# Char, big scalars, one-element vectors and matrices as guard expression.
# Big integers and rationals always succeed: because of
# value shrinking zero is always a simple Int or Char.
op a test b =
 a:b
 "fail"

0 test "succeed"
1 test "succeed"
"a" test "succeed"
(char 0) test "succeed"
(!10) test "succeed"
1/10 test "succeed"
(sqrt 2) test "succeed"
(float 0) test "succeed"
(1 rho 1) test "succeed"
(1 rho 0) test "succeed"
(1 1 1 rho 1) test "succeed"
(1 1 1 rho 0) test "succeed"
	fail
	succeed
	succeed
	fail
	succeed
	succeed
	succeed
	fail
	succeed
	fail
	succeed
	fail

# Issue 119
op foo a = a[;1]
foo 3 3 rho iota 10
	1 4 7

# Undefine an operator.

op a mod b = 7
5 mod 2
)clear mod
5 mod 2
	7
	1

op floor b = 7
floor 12
)clear floor
floor 12
	7
	12

# Undefine a variable (not really about functions but let's keep the clear tests together.)
x = 4
y = 3
)vars
)clear var x
)vars
)clear vars
)vars
	e
	pi
	x
	y
	e
	pi
	y
	e
	pi

# user-defined used to not be recognized mid-expression unless also binary
op nop x = x
+/ nop 1 2 3
+/ nop@ 1 2 3
	6
	6

# each modifier (@)
1 2 3 ,@ 4 5 6
	(1 2 3 4) (1 2 3 5) (1 2 3 6)

mix 1 2 3 ,@ 4 5 6
	1 2 3 4
	1 2 3 5
	1 2 3 6

mix 1 2 3 @, 4 5 6
	1 4 5 6
	2 4 5 6
	3 4 5 6

mix 1 2 3 @,@ 4 5 6
	1 4
	1 5
	1 6

	2 4
	2 5
	2 6

	3 4
	3 5
	3 6

1 2 3 @,@ 4 5 6
	(1 4) (1 5) (1 6)
	(2 4) (2 5) (2 6)
	(3 4) (3 5) (3 6)

1 2 3 o., 4 5 6
	(1 4) (1 5) (1 6)
	(2 4) (2 5) (2 6)
	(3 4) (3 5) (3 6)

rho 1 2 3 @,@ 4 5 6
	3 3

rho mix 1 2 3 @,@ 4 5 6
	3 3 2

rho 1 2 3 o., 4 5 6
	3 3

mix 1 2 3 @, 4 5
	1 4 5
	2 4 5
	3 4 5

mix 1 2 3 @, 4
	1 4
	2 4
	3 4

1 2 3 @, 0 rho 1
	(1) (2) (3)

1 2 ,@ 3 4 5
	(1 2 3) (1 2 4) (1 2 5)

1 ,@ 2 3
	(1 2) (1 3)

(0 rho 1) ,@ 2 3
	(2) (3)

x = 2 4 4 rho ".....\\/../\\.....", "...../\\..\\/....."; x
	....
	.\/.
	./\.
	....

	....
	./\.
	.\/.
	....

op d shift m = transp d[2] rot transp d[1] rot m
1 1 shift 4 4 rho ".....\\/../\\....."
	\/..
	/\..
	....
	....

op d shift m = transp d[2] rot transp d[1] rot m
mix 1 1 shift@ 2 4 4 rho ".....\\/../\\.....", "...../\\..\\/....."
	\/..
	/\..
	....
	....

	/\..
	\/..
	....
	....

op d shift m = transp d[2] rot transp d[1] rot m
mix (4 2 rho 1 1 1 -1 -1 1 -1 -1) @shift 4 4 rho ".....\\/../\\....."
	\/..
	/\..
	....
	....

	....
	....
	\/..
	/\..

	..\/
	../\
	....
	....

	....
	....
	..\/
	../\

op d shift m = transp d[2] rot transp d[1] rot m
mix (4 2 rho 1 1 1 -1 -1 1 -1 -1) @shift@ 2 4 4 rho ".....\\/../\\.....", "...../\\..\\/....."
	[1 * * *]:
	  \/..
	  /\..
	  ....
	  ....

	  /\..
	  \/..
	  ....
	  ....

	[2 * * *]:
	  ....
	  ....
	  \/..
	  /\..

	  ....
	  ....
	  /\..
	  \/..

	[3 * * *]:
	  ..\/
	  ../\
	  ....
	  ....

	  ../\
	  ..\/
	  ....
	  ....

	[4 * * *]:
	  ....
	  ....
	  ..\/
	  ../\

	  ....
	  ....
	  ../\
	  ..\/

iota@@ split split 3 2 1 rho iota 6
	((1) (1 2)) ((1 2 3) (1 2 3 4)) ((1 2 3 4 5) (1 2 3 4 5 6))

iota@@ 3 2 1 rho iota 6
	          (1)         (1 2)
	      (1 2 3)     (1 2 3 4)
	  (1 2 3 4 5) (1 2 3 4 5 6)

rho iota@@ 3 2 rho iota 6
	3 2

iota@@ 3 2 rho iota 6
	          (1)         (1 2)
	      (1 2 3)     (1 2 3 4)
	  (1 2 3 4 5) (1 2 3 4 5 6)

rho iota@@ 3 2 1 rho iota 6
	3 2

op nop x = x
rho nop@ 1 0 2 rho 1
rho nop@@ 1 0 2 rho 1
rho nop@@@ 1 0 2 rho 1
	0
	1 0
	1 0 2

op x left y = x
d = 1 2; d @left 1
	1 2

op x left y = x
d = 1 2; d@left 1
	1 2

op x left y = x
d = 1 2; 3 left@ d
	3 3

op x left y = x
d = 1 2; 3 left@d
	3 3

op x left y = x
d = 1 2; d @left@ d
	1 1
	2 2

op x left y = x
d = 1 2; d@left@d
	1 1
	2 2

op x left y = x
1@left 2
	1

x y = 100 200
op f (x y) = +/x y
f 1 2
x y
	3
	100 200

a b c d = 100*iota 4
op (a b) f (c d) = +/a b c d
1 2 f 3 4
a b c d
	10
	100 200 300 400

a b c d e g = 100*iota 6
op (a (b c)) f ((d e) g) = +/a b c d e g
1 (2 3) f (4 5) 6
a b c d e g
	21
	100 200 300 400 500 600

op x sub y = x-y
10 20 @sub@ 1 2
	 9  8
	19 18

10 20 @-@ 1 2
	 9  8
	19 18

op x sub y = x-y
10 20 sub@ 1 2
	(9 19) (8 18)

10 20 -@ 1 2
	(9 19) (8 18)

op x sub y = x-y
10 20 @sub 1 2
	(9 8) (19 18)

10 20 @- 1 2
	(9 8) (19 18)

op x f y = x+y
1 2 @f 3
	4 5

op x f y = x+y
x=1 2
x @f 3
	4 5

op x f y = x+y
(x=1 2) @f 3
	4 5

op x f y = x+y
3 f@ x=1 2
	4 5

op f x = -x
f@ 1 2
	-1 -2

op f x = -x
f@ x=1 2
	-1 -2

# Issue 211: Allow identifiers to mean a variable or operator according to context.
op f x =
    min = x-min/ 3 1 4 # operator and variable in same expression!
    x min x

f 2
max=40000
max/10
max / 10
	2 1 2
	4000
	4000

# Issue 211 cont'd: Decorators on undeclared ops.
op A x = 3 @add@ x
op B x = add/ x
op C x = add\ x
op D x = 3 o.add x
op E x = 3 add.sub x
op F x = 3 add.- x
op G x = 3 +.sub x
op left add right = left + right
op left sub right = left - right
A 3
B 3
C 3
D 3
E 3
F 3
G 3
	6
	3
	3
	6
	0
	0
	0

# Issue 211 cont'd: Can now use j as a variable
j = 1
j
	1

# A variable shadowing a predefined up requires a reparse.
op a x = 3 max 4
a 3
max = 2
a 3
	4
	3 2 4

# A calculation used while developing the code for gamma, kept as a
# test because it triggered an indexing bug. Plus we wanted to save it
# for posterity. In the end we went to a more accurate approximation.
)get testdata/lanczos
	1.000000000000000174663
	5716.400188274341379136
	-14815.30426768413909044
	14291.49277657478554025
	-6348.160217641458813289
	1301.608286058321874105
	-108.1767053514369634679
	2.605696505611755827729
	-0.007423452510201416151527
	5.384136432509564062961e-08
	-4.023533141268236372067e-09

# A related test for gamma using a variant of Spouge's approximation.
# This runs the code that generates the constants in fac.go and uses
# them to approximate ùö™(6). The result here is using the code
# transliterated into fac.go.
)get testdata/gamma
	#
	39916799.999999999999999999999999999999999999999908337720309466210041813686726577105514
